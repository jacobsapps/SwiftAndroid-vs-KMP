import java.nio.file.Files

plugins {
    id "com.android.library"
}

android {
    namespace "com.jacob.core"
    compileSdkVersion 34

    defaultConfig {
        minSdkVersion 28
    }
}

java {
    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11
}

dependencies {
    implementation('org.swift.swiftkit:swiftkit-core:1.0-SNAPSHOT')
}

def getSwiftlyPath() {
    def fromConfig = project.findProperty("swiftly.path") ?: System.getenv("SWIFTLY_PATH")
    if (fromConfig) {
        return file(fromConfig)
    }

    def homeDir = System.getProperty("user.home")
    def possiblePaths = [
        "$homeDir/.swiftly/bin/swiftly",
        "$homeDir/.local/share/swiftly/bin/swiftly",
        "$homeDir/.local/bin/swiftly",
        "/usr/local/bin/swiftly",
        "/opt/homebrew/bin/swiftly",
        "/root/.local/share/swiftly/bin/swiftly"
    ]

    for (path in possiblePaths) {
        if (file(path).exists()) {
            return path
        }
    }

    throw new GradleException("swiftly not found. Set swiftly.path or SWIFTLY_PATH.")
}


def getSwiftSDKPath() {
    def fromConfig = project.findProperty("swift.sdk.path") ?: System.getenv("SWIFT_SDK_PATH")
    if (fromConfig) {
        return file(fromConfig)
    }

    def homeDir = System.getProperty("user.home")
    def possiblePaths = [
        "$homeDir/Library/org.swift.swiftpm/swift-sdks/",
        "$homeDir/.config/swiftpm/swift-sdks/",
        "$homeDir/.swiftpm/swift-sdks/",
        "/root/.swiftpm/swift-sdks/"
    ]

    for (path in possiblePaths) {
        if (file(path).exists()) {
            return file(path)
        }
    }

    throw new GradleException("Swift SDK path not found. Set swift.sdk.path or SWIFT_SDK_PATH.")
}

def swiftRuntimeLibs = [
    "swiftCore",
    "swift_Concurrency",
    "swift_StringProcessing",
    "swift_RegexParser",
    "swift_Builtin_float",
    "swift_math",
    "swiftAndroid",
    "dispatch",
    "BlocksRuntime",
    "swiftSwiftOnoneSupport",
    "swiftDispatch",
    "Foundation",
    "FoundationEssentials",
    "FoundationInternationalization",
    "FoundationNetworking",
    "_FoundationICU",
    "swiftSynchronization"
]

def sdkName = "swift-DEVELOPMENT-SNAPSHOT-2025-10-16-a-android-0.1.artifactbundle"
def swiftVersion = "main-snapshot-2025-10-16"
def minSdk = android.defaultConfig.minSdkVersion.apiLevel


def abis = [
    "arm64-v8a": [triple: "aarch64-unknown-linux-android${minSdk}", androidSdkLibDirectory: "swift-aarch64", ndkDirectory: "aarch64-linux-android"],
    "x86_64": [triple: "x86_64-unknown-linux-android${minSdk}", androidSdkLibDirectory: "swift-x86_64", ndkDirectory: "x86_64-linux-android"]
]

def generatedJniLibsDir = layout.buildDirectory.dir("generated/jniLibs")
def generatedJavaDir = layout.buildDirectory.dir("generated/java")
def swiftSdkPath = "${getSwiftSDKPath().absolutePath}/${sdkName}"

def buildSwiftAll = tasks.register("buildSwiftAll") {
    group = "build"
    description = "Builds the Swift code for all Android ABIs."

    inputs.file(new File(projectDir, "Package.swift"))
    inputs.dir(new File(layout.projectDirectory.asFile, "Sources/RollerCoasterCore".toString()))

    outputs.dir(layout.buildDirectory.dir("../.build/plugins/outputs/${layout.projectDirectory.asFile.name.toLowerCase()}"))
}

abis.each { abi, info ->
    def task = tasks.register("buildSwift${abi.capitalize()}", Exec) {
        group = "build"
        description = "Builds the Swift code for the ${abi} ABI."

        doFirst {
            println("Building Swift for ${abi} (${info.triple})...")
            layout.projectDirectory.dir(".build-${abi}").asFile.deleteDir()
        }

        outputs.dir(layout.projectDirectory.dir(".build-${abi}/${info.triple}/debug"))

        workingDir = layout.projectDirectory
        executable(getSwiftlyPath())
        args("run", "swift", "build", "+${swiftVersion}", "--product", "RollerCoasterCore", "--swift-sdk", info.triple, "--scratch-path", ".build-${abi}")

        environment("JAVA_HOME", System.getenv("JAVA_HOME"))
    }

    buildSwiftAll.configure { dependsOn(task) }
}


def copyJniLibs = tasks.register("copyJniLibs", Copy) {
    dependsOn(buildSwiftAll)

    abis.each { abi, info ->
        from(layout.projectDirectory.dir(".build-${abi}/${info.triple}/debug")) {
            include("*.so")
            into(abi)
        }

        from(file("${swiftSdkPath}/swift-android/ndk-sysroot/usr/lib/${info.ndkDirectory}/libc++_shared.so")) {
            into(abi)
        }

        doFirst {
            println("Copying Swift runtime libraries for ${abi}...")
        }

        from(swiftRuntimeLibs.collect { libName ->
            "${swiftSdkPath}/swift-android/swift-resources/usr/lib/${info.androidSdkLibDirectory}/android/lib${libName}.so"
        }) {
            into(abi)
        }
    }

    into(generatedJniLibsDir)

    doLast {
        def candidateSources = abis.keySet().collect { abi ->
            layout.projectDirectory.dir(".build-${abi}/plugins/outputs/${layout.projectDirectory.asFile.name.toLowerCase()}/RollerCoasterCore/destination/JExtractSwiftPlugin/src/generated/java").asFile
        }
        candidateSources += layout.projectDirectory.dir(".build/plugins/outputs/${layout.projectDirectory.asFile.name.toLowerCase()}/RollerCoasterCore/destination/JExtractSwiftPlugin/src/generated/java").asFile
        def generatedSourceDir = candidateSources.find { it.exists() }
        def targetDir = generatedJavaDir.get().asFile
        project.delete(targetDir)
        targetDir.mkdirs()
        if (generatedSourceDir.exists()) {
            project.copy {
                from(generatedSourceDir)
                into(targetDir)
            }
        }

        def appJniLibs = layout.projectDirectory.dir("../app/src/main/jniLibs").asFile
        project.delete(appJniLibs)
        appJniLibs.mkdirs()
        project.copy {
            from(generatedJniLibsDir)
            into(appJniLibs)
        }
    }
}

android {
    sourceSets {
        main {
            java {
                srcDir(buildSwiftAll.map { it.outputs.files })
                srcDir(generatedJavaDir)
            }
            jniLibs {
                srcDir(generatedJniLibsDir)
            }
        }
    }
}

tasks.named("preBuild") {
    dependsOn(copyJniLibs)
}
